plugins {
    id 'java-library'
    id 'maven-publish'

    id 'net.minecrell.licenser' version '0.4.1'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withJavadocJar()
    withSourcesJar()
}

group = 'org.cadixdev'
archivesBaseName = project.name.toLowerCase()
version = '0.1.0-SNAPSHOT'

repositories {
    mavenCentral()
    if (mercuryVersion.endsWith("-SNAPSHOT")) {
        maven {
            url 'https://oss.sonatype.org/content/groups/public/'
        }
    }
}

dependencies {
    api "org.cadixdev:mercury:$mercuryVersion"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
}

test {
    useJUnitPlatform()
}

license {
    header = project.file('HEADER.txt')

    exclude 'org/cadixdev/mercury/mixin/annotation/AccessorName.java'
}

processResources {
    from 'LICENSE.txt'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.archivesBaseName
            from components.java

            pom {
                name = project.name
                description = project.description
                packaging = 'jar'
                url = project.url
                inceptionYear = project.inceptionYear

                scm {
                    url = 'https://github.com/CadixDev/MercuryMixin'
                    connection = 'scm:git:https://github.com/CadixDev/MercuryMixin.git'
                    developerConnection = 'scm:git:git@github.com:CadixDev/MercuryMixin.git'
                }

                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/CadixDev/MercuryMixin/issues'
                }

                licenses {
                    license {
                        name = 'Mozilla Public License 2.0'
                        url = 'https://opensource.org/licenses/MPL-2.0'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'jamierocks'
                        name = 'Jamie Mansfield'
                        email = 'jmansfield@cadixdev.org'
                        url = 'https://www.jamiemansfield.me/'
                        timezone = 'Europe/London'
                    }
                }
            }
        }
    }
    repositories {
        if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
            maven {
                url = !version.endsWith('-SNAPSHOT') ?
                        'https://oss.sonatype.org/service/local/staging/deploy/maven2/' :
                        'https://oss.sonatype.org/content/repositories/snapshots/'

                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }
}

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
    apply plugin: 'signing'
    signing {
        required { !version.endsWith('-SNAPSHOT') && gradle.taskGraph.hasTask(tasks.publish) }
        sign publishing.publications.mavenJava
    }
}
